/*
 * Created by Ezra Lazuardy on 10/31/19, 12:23 AM
 * Copyright (c) 2019 . All rights reserved.
 * Last modified 10/31/19, 12:13 AM
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlinx-serialization'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    //noinspection GroovyMissingReturnStatement
    defaultConfig {
        applicationId "com.muvi"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
        vectorDrawables.useSupportLibrary = true
        buildConfigField("String", "TMDB_API_KEY", TMDB_API_KEY)
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                    "room.schemaLocation" : "$projectDir/schemas".toString(),
                    "room.incremental" : "true",
                    "room.expandProjection" : "true"
                ]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    //noinspection GroovyMissingReturnStatement
    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
    }

    androidExtensions {
        experimental = true
    }

    //noinspection GroovyMissingReturnStatement
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
        unitTests.includeAndroidResources = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Androidx
    implementation "androidx.appcompat:appcompat:$androidx"
    implementation "androidx.vectordrawable:vectordrawable:$androidx"
    implementation "androidx.constraintlayout:constraintlayout:${androidx_constraint}"

    // Androidx Lifecycle Components
    kapt "androidx.lifecycle:lifecycle-common-java8:$androidx_lifecycle"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidx_lifecycle"
    implementation "androidx.lifecycle:lifecycle-extensions:$androidx_lifecycle"
    androidTestImplementation "androidx.arch.core:core-testing:$androidx_arch"

    // Android Material
    implementation "com.google.android.material:material:$android_material"

    // Anko
    implementation "org.jetbrains.anko:anko:$anko"
    implementation "org.jetbrains.anko:anko-commons:$anko"
    implementation "org.jetbrains.anko:anko-design:$anko"

    // ElasticView
    implementation "com.github.armcha:ElasticView:$elastic_view"

    // Glide
    implementation "com.github.bumptech.glide:glide:$glide"

    // Koin
    implementation "org.koin:koin-test:$koin"
    implementation "org.koin:koin-android:$koin"
    implementation "org.koin:koin-android-scope:$koin"
    implementation "org.koin:koin-android-viewmodel:$koin"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.12.0"

    // Kotlin Coroutines
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines"

    // Ktx
    implementation "androidx.core:core-ktx:$ktx"
    implementation "androidx.fragment:fragment-ktx:$fragment_ktx"
    implementation "androidx.work:work-runtime-ktx:$work"

    // Lottie
    implementation "com.airbnb.android:lottie:$lottie"

    // MaterialSearchView
    implementation "com.miguelcatalan:materialsearchview:1.4.0"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit"

    // Room
    kapt "androidx.room:room-compiler:$room"
    implementation "androidx.room:room-runtime:$room"
    implementation "androidx.room:room-ktx:$room"
    androidTestImplementation "androidx.room:room-testing:$room"

    // Rounded ImageView
    implementation "com.makeramen:roundedimageview:$rounded_image_view"

    // ThreeTen Android Backport
    implementation "com.jakewharton.threetenabp:threetenabp:$threetenabp"

    testImplementation "junit:junit:$junit"
    testImplementation "androidx.test:core:$test_core"
    testImplementation "androidx.test:runner:$test_runner"
    testImplementation "androidx.test.espresso:espresso-core:$espresso"
    testImplementation "androidx.arch.core:core-testing:$androidx_core_test"
    testImplementation "org.robolectric:robolectric:$robolectric"
    testImplementation group: "org.mockito", name: "mockito-core", version: "$mockito"
    androidTestImplementation "androidx.test:runner:$test_runner"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso"
    androidTestImplementation "androidx.arch.core:core-testing:$androidx_core_test"
}
